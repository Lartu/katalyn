(* Advent of Code 2023 - Day 2, Part 1 *)

def append;
    in $table: $_[1];
    in $table[len($_[1]) + 1]: $_[2];
    return $table;
ok;

def split;
    in $string: $_[1];
    in $separator: $_[2];
    in $max_split: -1;
    if $_[3];
        in $max_split: $_[3];
    ok;
    in $token: "";
    in $tokens: table;
    if len($string) < len($separator);
        return append($tokens, $string);
    ok;
    in $i: 1;
    while slice($string, $i, len($separator));
        in $result: 0;
        if $_r = $separator;
            in $result: $_r;
            in $i: $i + len($separator);
            if len($token);
                append($tokens, $token);
                in $token: "";
                if len($tokens) = $max_split;
                    append($tokens, slice($string, $i));
                    break;
                ok;
            ok;
            continue;
        ok;
        if !$result;
            in $token: $token & slice($string, $i, 1);
        ok;
        in $i: $i + 1;
    ok;
    if len($token);
        append($tokens, $token);
    ok;
    return $tokens;
ok;

def init_table;
    in $table: table;
    in $i: 1;
    in $key: "";
    while $_[$i];
        in $token: $_r;
        if $i % 2 = 1;
            in $key: $token;
        ok;
        if $i % 2 = 0;
            in $table[$key]: $token;
        ok;
        in $i: $i + 1;
    ok;
    return $table;
ok;

(* --- *)

in $bag_contents: init_table(
    "red", 12,
    "green", 13,
    "blue", 14
);

def is_game_possible;
    in $result: table;
    in $game_str: $_[1];
    in $result{game_id}: trim(split(slice($game_str, 5), ":", 1)[1]);
    in $result{possible}: 1;
    in $plays: split(split($game_str, ":", 1)[2], ";");
    in $i: 1;
    while $plays[$i];
        in $cubes: split($_r, ",");
        in $j: 1;
        while $cubes[$j];
            in $count_cube: split(trim($_r), " ");
            if $count_cube[1] > $bag_contents[$count_cube[2]];
                in $result{possible}: 0;
                break 2;
            ok;
            in $j: $j + 1;
        ok;
        in $i: $i + 1;
    ok;
    return $result;
ok;

in $file: open_rw("aoc2_input.txt");
in $sum: 0;
while read_line($file);
    in $game_result: is_game_possible($_r);
    if $game_result{possible};
        in $sum: $sum + $game_result{game_id};
    ok;
ok;
print("Total ID Sum: ", $sum);